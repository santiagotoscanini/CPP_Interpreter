;-- Program
PDefs.   Program ::= [Def] ;

;-- def. Functions, using, typedef
DFun.       Def ::= Type Id "(" [Arg] ")" "{" [Stm] "}" ;
DFunSBdy.   Def ::= Type Id "(" [Arg] ")" ";" ;
DFunInline. Def ::= "inline" Type Id "(" [Arg] ")" "{" [Stm] "}" ;
DFunSBdyIL. Def ::= "inline" Type Id "(" [Arg] ")" ";" ;
DUs.        Def ::= "using" QConst ";" ;
DType.      Def ::= "typedef" Type Id ";" ;
DDecls.     Def ::= Decl ;
DStruct.    Def ::= "struct" Id "{" [Decl] "}" ";";

terminator Def "";

;-- Declaration
Decl. Decl ::=  Type [Id] ";" ;
DInit.Decl ::=  Type Id "=" Exp ";";

separator       Decl "" ;

;-- Arguments
ADeclBlnck.     Arg1 ::= Type ;
ADecl.          Arg1 ::= Type Id ;
ADeclIn.        Arg1 ::= Type Id "=" Exp15 ;
AConst.         Arg ::= "const" Arg1 ;
ATodos.         Arg ::= Arg1 ;

separator       Arg "," ;

;-- Statements
SEmpty.      Stm ::= ";" ;
SExp.        Stm ::= Exp ";" ;
SDecls.      Stm ::= Decl ;
SReturn.     Stm ::= "return" Exp ";" ;
SReturnVoid. Stm ::= "return" ";" ;
SWhile.      Stm ::= "while" "(" Exp ")" Stm ;
SDoWhile.    Stm ::= "do" Stm "while" "(" Exp ")" Stm ;
SBlock.      Stm ::= "{" [Stm] "}" ;
SIf.         Stm ::= "if" "(" Exp ")" Stm ;
SIfElse.     Stm ::= "if" "(" Exp ")" Stm "else" Stm ;
SFor.        Stm ::= "for" "(" Decl Exp ";" Exp ")" Stm ;
SType.       Stm ::= "typedef" Type Id ";" ;
SConst.      Stm ::= "const" Type Exp2 ";" ;
SStruct.     Stm ::= "struct" Id "{" [Decl] "}" ";";

terminator Stm "" ;

;-- Qualified Constants (Classes)
QId.    QId ::= Id ;
QIdT.   QId ::= Id "<" [Type] ">" ;

QConst. QConst ::= [QId] ;

separator nonempty QId "::" ;

;-- Expressions
ETrue.   Exp16  ::= "true" ;
EFalse.  Exp16  ::= "false" ;
EInt.    Exp16  ::= Integer ;
EDouble. Exp16  ::= Double ;
EChar.   Exp16  ::= Char ;
EString. Exp16  ::= [String] ;
separator nonempty String "" ;

EQct.    Exp15  ::= QConst ;
EArr.    Exp15  ::= Id "[" Exp "]";
EApp.    Exp15  ::= Id "(" [Exp] ")" ;

EPEstr.  Exp14  ::= Exp15 "." Exp15 ;
EProy.   Exp14  ::= Exp15 "->" Exp15 ;
EPIncr.  Exp14  ::= Exp15 "++" ;
EPDecr.  Exp14  ::= Exp15 "--" ;
EPDecr.  Exp14  ::= "*" Exp15 ;

EIncr.   Exp13  ::= "++" Exp14 ;
EDecr.   Exp13  ::= "--" Exp14 ;
ENeg.    Exp13  ::= "!"  Exp14 ;

ETimes.  Exp12  ::= Exp12 "*"  Exp13 ;
EDiv.    Exp12  ::= Exp12 "/"  Exp13 ;
ERest.   Exp12  ::= Exp12 "%"  Exp13 ;

EPlus.   Exp11  ::= Exp11 "+"  Exp12 ;
EMinus.  Exp11  ::= Exp11 "-"  Exp12 ;

EShiftL. Exp10  ::= Exp10 "<<"  Exp11 ;
EShiftR. Exp10  ::= Exp10 ">>"  Exp11 ;

ELt.     Exp9   ::= Exp9  "<"  Exp10 ;
EGt.     Exp9   ::= Exp9  ">"  Exp10 ;
ELtEq.   Exp9   ::= Exp9  "<=" Exp10 ;
EGtEq.   Exp9   ::= Exp9  ">=" Exp10 ;

EEq.     Exp8   ::= Exp8  "==" Exp9 ;
ENEq.    Exp8   ::= Exp8  "!=" Exp9 ;

EAnd.    Exp4   ::= Exp4  "&&" Exp5 ;

EOr.     Exp3   ::= Exp3  "||" Exp4 ;

EMinAss. Exp2   ::= Exp3 "-=" Exp2 ;
EAddAss. Exp2   ::= Exp3 "+=" Exp2 ;
EAss.    Exp2   ::= Exp3 "=" Exp2 ;
ECond.   Exp2   ::= Exp3 "?" Exp2 ":" Exp2 ;

EExcp.   Exp1   ::= "throw" Exp2 ;

internal ETyped. Exp15 ::= "(" Exp ":" Type ")" ; -- que hace esta linea, que es internal
coercions Exp 16 ;
separator Exp "," ;

;-- Types
rules BuilInType   ::= "bool" | "int" | "double" | "void" ;

TypeB. Type ::= BuilInType ;
TypeQ. Type ::= QConst;
TypeP. Type ::= Type "&";
separator nonempty Type "," ;

;-- Names
token Id (letter (letter | digit | '_' )*) ;
separator nonempty Id "," ;

;-- Comments
comment "#" ;
comment "//" ;
comment "/*" "*/" ;
